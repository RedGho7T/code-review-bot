plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.13'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.groviate'
version = '1.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    telegrambotsVersion = '9.2.1'
    springAiVersion = '1.1.0'
    okhttpVersion = '4.12.0'
    logstashVersion = '7.4'
    jacocoVersion = '0.8.12'
    resilience4jVersion = "2.3.0"
}

jacoco {
    toolVersion = jacocoVersion
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

dependencies {
    // Spring Boot starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Telegram Bot API
    implementation "org.telegram:telegrambots-springboot-longpolling-starter:${telegrambotsVersion}"
    implementation "org.telegram:telegrambots-client:${telegrambotsVersion}"
    implementation "org.telegram:telegrambots-longpolling:${telegrambotsVersion}"

    implementation "org.apache.commons:commons-lang3:3.20.0"

    // DB & migrations
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql'

    // H2 для тестов
    testImplementation 'com.h2database:h2'

    // Spring AI
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'

    // HTTP / integration
    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"

    // Logging
    implementation "net.logstash.logback:logstash-logback-encoder:${logstashVersion}"

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Config metadata (для подсказок в application.yml)
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Dev
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"

    // Нужно для работы аннотаций Resilience4j через Spring AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Resilience4j для Spring Boot 3
    implementation "io.github.resilience4j:resilience4j-spring-boot3:${resilience4jVersion}"
    implementation "io.github.resilience4j:resilience4j-micrometer:${resilience4jVersion}"

    // Prometheus endpoint /actuator/prometheus
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
}

def halfCores = (Runtime.runtime.availableProcessors().intdiv(2) ?: 1)

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    systemProperty 'spring.profiles.active', 'test'

    // Токен можно переопределить из реального окружения, локально будет "test-token"
    environment 'GITLAB_API_TOKEN', (System.getenv('GITLAB_API_TOKEN') ?: 'test-token')

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}

test {
    maxParallelForks = halfCores
    finalizedBy jacocoTestReport
}

// Модульные тесты: src/test/java/**/unit/**
tasks.register('unitTest', Test) {
    description = 'Runs unit tests from the unit folder.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include '**/unit/**'
    maxParallelForks = halfCores
    shouldRunAfter test
    finalizedBy jacocoUnitTestReport

    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/unitTest.exec").get().asFile
    }
}

// Интеграционные тесты: src/test/java/**/integration/** + *ApplicationTests*
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests from the integration folder.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    include '**/integration/**', '**/*ApplicationTests*'
    maxParallelForks = 1
    shouldRunAfter test
    finalizedBy jacocoIntegrationTestReport

    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/integrationTest.exec").get().asFile
    }
}

tasks.register('jacocoUnitTestReport', JacocoReport) {
    dependsOn unitTest
    reports {
        xml.required = true
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/jacocoUnitTestReport')
    }

    executionData.from(layout.buildDirectory.file("jacoco/unitTest.exec"))

    sourceDirectories.setFrom files(sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(sourceSets.main.output.classesDirs)
}

tasks.register('jacocoIntegrationTestReport', JacocoReport) {
    dependsOn integrationTest
    reports {
        xml.required = true
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/jacocoIntegrationTestReport')
    }

    executionData.from(layout.buildDirectory.file("jacoco/integrationTest.exec"))

    sourceDirectories.setFrom files(sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(sourceSets.main.output.classesDirs)
}

// Общий отчет Jacoco
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/jacocoTestReport')
    }
}